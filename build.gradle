buildscript {
//    apply from: 'https://raw.githubusercontent.com/noeppi-noeppi/ModUtils/v2/buildscript.gradle', to: buildscript
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://libraries.minecraft.net/' }
        maven { url = 'https://maven.parchmentmc.org/' }
        maven { url = 'https://plugins.gradle.org/m2/' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url = 'https://noeppi-noeppi.github.io/MinecraftUtilities/maven/' }
        mavenCentral()
    }

    dependencies {
        classpath('net.minecraftforge.gradle:ForgeGradle') { version { strictly '5.1.23' } }
        classpath('io.github.noeppi_noeppi.tools:ModGradle') { version { strictly '1.0.39' } }
        classpath('org.parchmentmc:librarian') { version { strictly '1.1.4' } }
        classpath('gradle.plugin.com.matthewprenger:CurseGradle') { version { strictly '1.4.0' } }
        classpath('gradle.plugin.com.modrinth.minotaur:Minotaur') { version { strictly '1.2.1' } }
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'org.apache.maven:maven-artifact:3.8.1'
        classpath 'com.google.code.findbugs:jsr305:3.0.1'
    }
}

apply from: 'https://raw.githubusercontent.com/noeppi-noeppi/ModUtils/v2/mod.gradle'

apply plugin: 'io.github.noeppi_noeppi.tools.modgradle.javadoc'
apply plugin: 'io.github.noeppi_noeppi.tools.modgradle.mergeartifact'
apply plugin: 'io.github.noeppi_noeppi.tools.modgradle.coremods'

repositories {
    maven { url = 'https://maven.melanx.de' }
    maven { url = 'file:///home/tux/dev/util/MinecraftUtilities/maven/' }
}

dependencies {
    annotationProcessor "io.github.noeppi_noeppi.tools:LibXAP:${project.ap_version}"
    implementation "io.github.noeppi_noeppi.tools:LibXAP:${project.ap_version}"
    coremods 'io.github.noeppi_noeppi.misc:CoreModTypes:5.0.1-1'
}

mergeArtifacts {
    include "io.github.noeppi_noeppi.tools:LibXAP:${project.ap_version}"
}

javadocConfigure {
    exclude '**.impl.*?'
    exclude '**.annotation.processor.*?'
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.destinationDirectory(file("build/docs/javadoc"))
    title("LibX ${base_version} JavaDoc")
    options.windowTitle("LibX ${base_version} JavaDoc")
    options.header('<h3><a href="https://www.curseforge.com/minecraft/mc-mods/libx" target="_blank">CurseForge</a>' \
            + '&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;' \
            + '<a href="https://github.com/noeppi-noeppi/LibX" target="_blank">GitHub</a></h2>')
    options.memberLevel = JavadocMemberLevel.PROTECTED
}

// Performs checks for releases
task release {
    finalizedBy compileJava
    
    doFirst {
        compileJava.options.compilerArgs += [
                '-Amod.properties.release=true'
        ]
    }
}
buildscript {
    apply from: 'https://noeppi-noeppi.github.io/ModUtils/v3/buildscript.gradle', to: buildscript
}

apply from: 'https://noeppi-noeppi.github.io/ModUtils/v3/mod.gradle'

apply plugin: 'io.github.noeppi_noeppi.tools.modgradle.javadoc'
apply plugin: 'io.github.noeppi_noeppi.tools.modgradle.coremods'

configurations {
    apCompileOnly
    apCompileOnlyResolvable.extendsFrom apCompileOnly
}

dependencies {
    apCompileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

sourceSets {
    ap {
        compileClasspath = configurations.apCompileOnlyResolvable
    }
}

project.addModSourceSet(sourceSets.ap)

dependencies {
    annotationProcessor sourceSets.ap.output
    implementation sourceSets.ap.output
    
    coremods 'io.github.noeppi_noeppi.misc:CoreModTypes:5.0.2-1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
}

test {
    useJUnitPlatform()
}

jar {
    from sourceSets.ap.output
}

sourceJarConfigure {
    sourceSets.ap.java.srcDirs.forEach { additionalSourceDir it }
}

javadocConfigure {
    from sourceSets.ap.java.sourceDirectories
    exclude '**.impl.*?'
    exclude '**.annotation.processor.*?'
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.destinationDirectory(file("build/docs/javadoc"))
    title("LibX ${base_version} JavaDoc")
    options.windowTitle("LibX ${base_version} JavaDoc")
    options.header('<h3><a href="https://noeppi-noeppi.github.io/WikiX" target="_blank">Wiki</a>' \
            + '&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;' \
            + '<a href="https://github.com/noeppi-noeppi/LibX" target="_blank">GitHub</a></h2>')
    options.memberLevel = JavadocMemberLevel.PROTECTED
}

// Performs checks for releases
task release {
    finalizedBy compileJava
    
    doFirst {
        compileJava.options.compilerArgs += [
                '-Amod.properties.release=true'
        ]
    }
}
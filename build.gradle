buildscript {
    apply from: 'https://moddingx.github.io/ModUtils/v4/buildscript.gradle', to: buildscript
}

apply from: 'https://moddingx.github.io/ModUtils/v4/mod.gradle'

apply plugin: 'org.moddingx.modgradle.javadoc'
apply plugin: 'org.moddingx.modgradle.coremods'

configurations {
    apCompileOnly
    apCompileOnlyResolvable.extendsFrom apCompileOnly
}

dependencies {
    apCompileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

sourceSets {
    ap {
        compileClasspath = configurations.apCompileOnlyResolvable
    }
}

project.addModSourceSet(sourceSets.ap)

dependencies {
    annotationProcessor sourceSets.ap.output
    implementation sourceSets.ap.output
    
    coremods 'org.moddingx:CoreModTypes:5.0.2-2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'net.minecraftforge:srgutils:0.4.13'
    testImplementation 'com.moandjiezana.toml:toml4j:0.7.2'
}

compileJava.options.compilerArgs += [
        '-Amod.properties.strict_onlyin=true',
        '-Amod.properties.strict_super=true',
        '-Amod.properties.force_findbugs=true'
]

test {
    dependsOn createSrgToMcp // Needed, so the tests can load mappings
    useJUnitPlatform()
}

jar {
    from sourceSets.ap.output
}

sourceJarConfigure {
    sourceSets.ap.java.srcDirs.forEach { additionalSourceDir it }
}

javadocConfigure {
    from sourceSets.ap.java.sourceDirectories
    exclude '**.impl.*?'
    exclude '**.annotation.processor.*?'
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.destinationDirectory(file("build/docs/javadoc"))
    title("LibX ${base_version} JavaDoc")
    options.windowTitle("LibX ${base_version} JavaDoc")
    options.header('<h3><a href="https://moddingx.github.io/WikiX" target="_blank">Wiki</a>' \
            + '&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;' \
            + '<a href="https://github.com/ModdingX/LibX" target="_blank">GitHub</a></h2>')
    options.memberLevel = JavadocMemberLevel.PROTECTED
}

// Performs checks for releases
task release {
    finalizedBy compileJava
    
    doFirst {
        compileJava.options.compilerArgs += [
                '-Amod.properties.release=true'
        ]
    }
}

repositories {
    mavenCentral()
}
